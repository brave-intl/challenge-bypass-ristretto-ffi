declare_args() {
  challenge_bypass_shared_lib = is_mac
  challenge_bypass_rust_flags = ""
}

if (challenge_bypass_shared_lib && challenge_bypass_rust_flags == "") {
  if (!is_component_build) {
    challenge_bypass_rust_flags = "-C link-args=-Wl,-install_name,@loader_path/../../Libraries/libchallenge_bypass_ristretto.dylib"
  }
}

challenge_bypass_build_path = "$root_gen_dir/challenge_bypass_ristretto/out"

challenge_bypass_lib_path = challenge_bypass_build_path

challenge_bypass_target = ""

# See https://forge.rust-lang.org/platform-support.html for possible targets
if (is_win) {
  if (target_cpu == "x86") {
    challenge_bypass_target = "i686-pc-windows-msvc"
  } else if (target_cpu == "x64") {
    challenge_bypass_target = "x86_64-pc-windows-msvc"
  }
} else if (is_mac) {
  if (target_cpu == "x64") {
    challenge_bypass_target = "x86_64-apple-darwin"
  }
} else if (is_linux) {
  if (target_cpu == "x64") {
    challenge_bypass_target = "x86_64-unknown-linux-gnu"
  }
} else if (is_android) {
  if (target_cpu == "arm") {
    challenge_bypass_target = "arm-linux-androideabi"
  } else if (target_cpu == "arm64") {
    challenge_bypass_target = "aarch64-linux-android"
  } else if (target_cpu == "x86") {
    challenge_bypass_target = "i686-linux-android"
  } else if (target_cpu == "x64") {
    challenge_bypass_target = "x86_64-linux-android"
  }
} else if (is_ios) {
  if (target_cpu == "arm64") {
    challenge_bypass_target = "aarch64-apple-ios"
  } else if (target_cpu == "x64") {
    challenge_bypass_target = "x86_64-apple-ios"
  }
}

if (challenge_bypass_target != "") {
  challenge_bypass_lib_path += "/" + challenge_bypass_target
}

if (is_debug) {
  challenge_bypass_lib_path += "/debug"
} else {
  challenge_bypass_lib_path += "/release"
}

if (is_win) {
  challenge_bypass_lib_path += "/challenge_bypass_ristretto.lib"
} else if (is_linux || is_android || is_ios) {
  challenge_bypass_lib_path += "/libchallenge_bypass_ristretto.a"
} else if (is_linux || is_android || is_ios || is_mac) {
  if (challenge_bypass_shared_lib) {
    challenge_bypass_lib_path += "/libchallenge_bypass_ristretto.dylib"
  } else {
    challenge_bypass_lib_path += "/libchallenge_bypass_ristretto.a"
  }
}
