# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("config.gni")
import("//brave/build/cargo.gni")
import("//brave/test/testing.gni")

config("external_config") {
  include_dirs = [ "src" ]
  defines = [
    "NO_CXXEXCEPTIONS=1"
  ]
}

source_set("challenge_bypass_ristretto_ffi") {
  public_configs = [ ":external_config" ]

  sources = [
    "src/wrapper.cpp",
    "src/wrapper.hpp"
  ]

  deps = [
    "//base",
    ":challenge_bypass_ristretto"
  ]

  libs = [ challenge_bypass_lib_path ]

  if (is_mac || is_ios ) {
    libs += [
      "Security.framework"
    ]
  }
}


if (challenge_bypass_shared_lib_install_name != "") {
  fix_testing_install_name("test_install_name") {
    current_path = challenge_bypass_shared_lib_install_name
    new_path = "@executable_path/" + rebase_path(challenge_bypass_lib_path, root_build_dir)
  }
}

cargo_build("challenge_bypass_ristretto") {
  inputs = [
    "Cargo.toml",
    "src/lib.h",
    "src/lib.rs",
  ]

  rust_flags = challenge_bypass_rust_flags
  output = challenge_bypass_lib_path

  manifest_path = "Cargo.toml"

  build_path = challenge_bypass_build_path

  target = challenge_bypass_target
}
