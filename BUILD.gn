# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("config.gni")
import("//brave/build/cargo.gni")

config("external_config") {
  include_dirs = [ "src" ]

  if (!challenge_bypass_use_exceptions) {
    defines = [
      "NO_CXXEXCEPTIONS=1"
    ]
  }
}

source_set("challenge_bypass_ristretto_ffi") {
  public_configs = [ ":external_config" ]

  sources = [
    "src/wrapper.cpp",
    "src/wrapper.hpp"
  ]

  deps = [
    ":challenge_bypass_ristretto"
  ]

  if (!challenge_bypass_use_exceptions) {
    sources += [
      "src/wrapper_no_exceptions.h",
    ]

    if (challenge_bypass_use_thread_local_pointer) {
      sources += [ "src/wrapper_no_exceptions_thread_local.cc" ]
    } else {
      sources += [ "src/wrapper_no_exceptions_thread_local_pointer.cc", ]
      deps += [ "//base" ]
    }
  }

  libs = [ challenge_bypass_lib_path ]

  if (is_mac || is_ios ) {
    libs += [
      "Security.framework"
    ]
  }
}

cargo_build("challenge_bypass_ristretto") {
  inputs = [
    "Cargo.toml",
    "src/lib.h",
    "src/lib.rs",
  ]

  output = challenge_bypass_lib_path

  manifest_path = "Cargo.toml"

  build_path = challenge_bypass_build_path

  target = challenge_bypass_target
}
